#Использовать logos

Перем _НастройкиУровнейЛогов;
Перем _НастройкиАппендеров;

Перем НастройкиЛогов;

Процедура ПриИнициализацииПоделки(Поделка) Экспорт

	КаталогМодуля = ОбъединитьПути(ТекущийСценарий().Каталог, "../Классы");
	Поделка.ПросканироватьКаталог(КаталогМодуля);

	ИнициализацияЛогов();

КонецПроцедуры

Процедура ИнициализацияЛогов()

	НастройкиЛогов = Новый НастройкиЛогов(_НастройкиУровнейЛогов, _НастройкиАппендеров);

	КорневойУровень = _НастройкиУровнейЛогов["rootLogger"];
	
	Если НЕ КорневойУровень = Неопределено Тогда

		УстановитьПеременнуюСреды("LOGOS_LEVEL", КорневойУровень);
		Логирование.ОбновитьНастройки();

	КонецЕсли;

	Для Каждого ТекущийЛогУровень Из _НастройкиУровнейЛогов Цикл

		Если ТекущийЛогУровень.Ключ = "rootLogger" Тогда
			Продолжить;	
		КонецЕсли;

		Лог = Логирование.ПолучитьЛог(ТекущийЛогУровень.Ключ);
		
		Уровень = НастройкиЛогов.ПолучитьУровеньЛога(ТекущийЛогУровень.Значение);
		Если Не Уровень = Неопределено Тогда
			Лог.УстановитьУровень(Уровень);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

&Заготовка
Процедура ПриСозданииОбъекта(
	&Деталька(Значение = "logos.logger", ЗначениеПоУмолчанию = Неопределено) НастройкиУровнейЛогов,
	&Деталька(Значение = "logos.appender", ЗначениеПоУмолчанию = Неопределено) НастройкиАппендеров
	)
	_НастройкиАппендеров = ?(НастройкиАппендеров = Неопределено, Новый Соответствие(), НастройкиАппендеров);
	_НастройкиУровнейЛогов = ?(НастройкиУровнейЛогов = Неопределено, Новый Соответствие(), НастройкиУровнейЛогов);;
КонецПроцедуры