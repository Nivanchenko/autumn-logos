#Использовать logos

Перем _НастройкиУровнейЛогов;
Перем _НастройкиАппендеров;

Процедура ПриИнициализацииПоделки(Поделка) Экспорт

	КаталогМодуля = ОбъединитьПути(ТекущийСценарий().Каталог, "../Классы");
	Поделка.ПросканироватьКаталог(КаталогМодуля);

	ИнициализацияЛогов();

КонецПроцедуры

Процедура ИнициализацияЛогов()

	КорневойУровень = _НастройкиУровнейЛогов["rootLogger"];
	
	Если НЕ КорневойУровень = Неопределено И НЕ КорневойУровень["level"] = Неопределено Тогда

		УстановитьПеременнуюСреды("LOGOS_LEVEL", КорневойУровень["level"]);
		Логирование.ОбновитьНастройки();
	КонецЕсли;

	НастройкиЛогированияСтрокой = СформироватьСтрокуНастроек();
	Если ЗначениеЗаполнено(НастройкиЛогированияСтрокой) Тогда
		НастройкиЛогирования = Логирование.ПолучитьНастройки();
		НастройкиЛогирования.ПрочитатьИзСтроки(НастройкиЛогированияСтрокой);
	КонецЕсли;

КонецПроцедуры

&Заготовка
Процедура ПриСозданииОбъекта(
	&Деталька(Значение = "logos.logger", ЗначениеПоУмолчанию = Неопределено) НастройкиУровнейЛогов,
	&Деталька(Значение = "logos.appender", ЗначениеПоУмолчанию = Неопределено) НастройкиАппендеров
	)
	_НастройкиАппендеров = ?(НастройкиАппендеров = Неопределено, Новый Соответствие(), НастройкиАппендеров);
	_НастройкиУровнейЛогов = ?(НастройкиУровнейЛогов = Неопределено, Новый Соответствие(), НастройкиУровнейЛогов);;
КонецПроцедуры

Функция СформироватьСтрокуНастроек()
	Результат = Новый Массив();

	ДобавитьНастройкуУровнейЛогов(Результат);

	ДобавитьНастройкуАппендеров(Результат);

	Возврат СтрСоединить(Результат, Символы.ПС);
КонецФункции

Процедура ДобавитьНастройкуУровнейЛогов(МассивРезультата)

	Для Каждого ТекущийЛог Из _НастройкиУровнейЛогов Цикл

		Если ТекущийЛог.Значение["level"] = "DEFAULT" Тогда
			Продолжить;
		КонецЕсли;

		ТекущаяСтрока = "logger." + ТекущийЛог.Ключ + "=" + ТекущийЛог.Значение["level"];

		АппендерыЛога = ТекущийЛог.Значение["appenders"];
		Если НЕ АппендерыЛога = Неопределено Тогда
			ТекущаяСтрока = ТекущаяСтрока + ", " + АппендерыЛога;
		КонецЕсли;

		МассивРезультата.Добавить(ТекущаяСтрока);

	КонецЦикла;

КонецПроцедуры

Процедура ДобавитьНастройкуАппендеров(МассивРезультата)

	Для Каждого ТекАппендер Из _НастройкиАппендеров Цикл
		ИмяАппендера = "appender." + ТекАппендер.Ключ;
		Для Каждого ПараметрАппендера Из ТекАппендер.Значение Цикл
			Если ПараметрАппендера.Ключ = "type" Тогда
				МассивРезультата.Добавить(ИмяАппендера + "=" + ПараметрАппендера.Значение);
			Иначе
				МассивРезультата.Добавить(ИмяАппендера + "." + ПараметрАппендера.Ключ + "=" + ПараметрАппендера.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры