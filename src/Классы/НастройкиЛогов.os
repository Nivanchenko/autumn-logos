#Использовать logos

Перем _НастройкиУровнейЛогов;
Перем _НастройкиАппендеров;

Перем СоответсвиеУровнейЛогов;

&Желудь
Процедура ПриСозданииОбъекта(
	&Деталька(Значение = "logos.logger", ЗначениеПоУмолчанию = Неопределено) НастройкиУровнейЛогов,
	&Деталька(Значение = "logos.appender", ЗначениеПоУмолчанию = Неопределено) НастройкиАппендеров
	)
	_НастройкиАппендеров = ?(НастройкиАппендеров = Неопределено, Новый Соответствие(), НастройкиАппендеров);
	_НастройкиУровнейЛогов = ?(НастройкиУровнейЛогов = Неопределено, Новый Соответствие(), НастройкиУровнейЛогов);

	Инициализация();
КонецПроцедуры

Процедура Инициализация()
	СоответсвиеУровнейЛогов = Новый Соответствие();
	СоответсвиеУровнейЛогов.Вставить("DEBUG", УровниЛога.Отладка);
	СоответсвиеУровнейЛогов.Вставить("INFO", УровниЛога.Информация);
	СоответсвиеУровнейЛогов.Вставить("WARN", УровниЛога.Предупреждение);
	СоответсвиеУровнейЛогов.Вставить("ERROR", УровниЛога.Ошибка);
	СоответсвиеУровнейЛогов.Вставить("CRITICALERROR", УровниЛога.КритичнаяОшибка);
	СоответсвиеУровнейЛогов.Вставить("DISABLE", УровниЛога.Отключить);
КонецПроцедуры

Функция ПолучитьУровеньЛогаПоТексту(Знач УровеньЛогаТекстом) Экспорт
	
	УровеньЛогаТекстом = ВРег(УровеньЛогаТекстом);

	Если УровеньЛогаТекстом = "DEFAULT" Тогда 
		Возврат Неопределено;
	Иначе
		Уровень = СоответсвиеУровнейЛогов[УровеньЛогаТекстом];
		Если Уровень = Неопределено Тогда
			ВызватьИсключение СтрШаблон("Неизвестный уровень лога %1", УровеньЛогаТекстом);
		Иначе
			Возврат Уровень;
		КонецЕсли;
	КонецЕсли;

КонецФункции

Функция ПолучитьУровеньЛогаПоИмени(ИмяЛога) Экспорт

	ДанныеТекущегоЛога = _НастройкиУровнейЛогов[ИмяЛога];

	Если ДанныеТекущегоЛога = Неопределено ИЛИ (ТипЗнч(ДанныеТекущегоЛога) = Тип("Соответствие") И ДанныеТекущегоЛога["level"] = Неопределено) Тогда
		Возврат Неопределено;
	КонецЕсли;

	УровеньТекстом = ДанныеТекущегоЛога["level"];

	Уровень = ПолучитьУровеньЛогаПоТексту(УровеньТекстом);

	Возврат Уровень;
	
КонецФункции

Функция ПолучитьАппендерПоИмени(ИмяЛога) Экспорт

	ИменаАппендеров = ИменаАппендеровДляЛога(ИмяЛога);

	Результат = Новый Массив();

	Для Каждого ИмяАппендера из ИменаАппендеров Цикл
		НастройкиАппендера = _НастройкиАппендеров[ИмяАппендера];
		Если НастройкиАппендера = Неопределено Тогда
			ВызватьИсключение СтрШаблон("Для %1 не определены настройки аппендера", ИмяАппендера);
		КонецЕсли;
		Результат.Добавить(НастройкиАппендера);
	КонецЦикла;

	Возврат Результат;
КонецФункции

Функция ИменаАппендеровДляЛога(ИмяЛога)

	Массив = Новый Массив();

	ДанныеЛога = _НастройкиУровнейЛогов[ИмяЛога];

	Если ДанныеЛога = Неопределено Тогда
		Возврат Массив;
	КонецЕсли;

	Аппендеры = ДанныеЛога["appenders"];

	Если Аппендеры = Неопределено Тогда
		Возврат Массив;
	КонецЕсли;

	Для Каждого ТекИмя из СтрРазделить(Аппендеры, ",", Ложь) Цикл
		Массив.Добавить(СокрЛП(ТекИмя));
	КонецЦикла;

	Возврат Массив;

КонецФункции
