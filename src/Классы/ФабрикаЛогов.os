#Использовать logos

&Пластилин Перем НастройкиЛогов Экспорт;

&Дуб
Процедура ПриСозданииОбъекта()
КонецПроцедуры

&Завязь
&Характер("Компанейский")
Функция Лог(&Блестяшка ИмяЛогаПолное, &Блестяшка ИмяЛога) Экспорт

	Лог = Логирование.ПолучитьЛог(ИмяЛогаПолное);
	УстановитьУровеньЛогаПоИмени(Лог, ИмяЛога);
	ДобавитьАппендерПоИмени(Лог, ИмяЛога);
	Возврат Лог;

КонецФункции

Процедура УстановитьУровеньЛогаПоИмени(Лог, ИмяЛога) Экспорт

	Если ИмяЛога = "" Тогда
		Возврат;
	КонецЕсли;

	Уровень = НастройкиЛогов.ПолучитьУровеньЛогаПоИмени(ИмяЛога);

	Если НЕ Уровень = Неопределено Тогда
		Лог.УстановитьУровень(Уровень);
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьАппендерПоИмени(Лог, ИмяЛога) Экспорт

	Аппендеры = НастройкиЛогов.ПолучитьАппендерПоИмени(ИмяЛога);
	Если Аппендеры = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ТипАппендера = Аппендеры["type"];
	Если ТипАппендера = "ВыводЛогаВФайл" Тогда
		Путь = Аппендеры["file"];	
		Если НЕ ЗначениеЗаполнено(Путь) Тогда
			ВызватьИсключение СтрШаблон("Для лога %1 не заполнен файл вывода", ИмяЛога);
		КонецЕсли;
		ФайлЖурнала = Новый ВыводЛогаВФайл;
		ФайлЖурнала.ОткрытьФайл(Путь);
		Лог.ДобавитьСпособВывода(ФайлЖурнала);	
	КонецЕсли;

	УровеньТекстом = Аппендеры["level"]; 
	Если ЗначениеЗаполнено(УровеньТекстом) Тогда
		Уровень = НастройкиЛогов.ПолучитьУровеньЛогаПоТексту(УровеньТекстом);
		Если НЕ Уровень = Неопределено Тогда
			Лог.УстановитьУровень(Уровень);	
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры