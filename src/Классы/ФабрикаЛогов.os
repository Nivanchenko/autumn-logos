#Использовать logos

&Пластилин Перем НастройкиЛогов Экспорт;

&Дуб
Процедура ПриСозданииОбъекта()
КонецПроцедуры

&Завязь
&Характер("Компанейский")
Функция Лог(&Блестяшка ИмяЛогаПолное, &Блестяшка ИмяЛога) Экспорт

	Лог = Логирование.ПолучитьЛог(ИмяЛогаПолное);
	УстановитьУровеньЛогаПоИмени(Лог, ИмяЛога);
	ДобавитьАппендерПоИмени(Лог, ИмяЛога);
	Возврат Лог;

КонецФункции

Процедура УстановитьУровеньЛогаПоИмени(Лог, ИмяЛога) Экспорт

	Если ИмяЛога = "" Тогда
		Возврат;
	КонецЕсли;

	Уровень = НастройкиЛогов.ПолучитьУровеньЛогаПоИмени(ИмяЛога);

	Если НЕ Уровень = Неопределено Тогда
		Лог.УстановитьУровень(Уровень);
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьАппендерПоИмени(Лог, ИмяЛога) Экспорт

	Аппендеры = НастройкиЛогов.ПолучитьАппендерПоИмени(ИмяЛога);
	Если Аппендеры.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Для Каждого НастройкаАппендера Из Аппендеры Цикл
		ОбработатьАппендер(Лог, НастройкаАппендера, ИмяЛога);
	КонецЦикла;

КонецПроцедуры

Процедура ОбработатьАппендер(Лог, НастройкаАппендера, ИмяЛога)
	ТипАппендера = НастройкаАппендера["type"];
	УровеньТекстом = НастройкаАппендера["level"]; 
	Если ЗначениеЗаполнено(УровеньТекстом) Тогда
		Уровень = НастройкиЛогов.ПолучитьУровеньЛогаПоТексту(УровеньТекстом);
	КонецЕсли;	

	Если ТипАппендера = "ВыводЛогаВФайл" Тогда
		Путь = НастройкаАппендера["file"];	
		Если НЕ ЗначениеЗаполнено(Путь) Тогда
			ВызватьИсключение СтрШаблон("Для лога %1 не заполнен файл вывода", ИмяЛога);
		КонецЕсли;
		Аппенедр = Новый ВыводЛогаВФайл;
	Иначе
		Аппенедр = Новый ВыводЛогаВКонсоль
			
	КонецЕсли;

	Для Каждого КиЗ Из НастройкаАппендера Цикл
		Если КиЗ.Ключ = "type" ИЛИ КиЗ.Ключ = "level" Тогда
			Продолжить;
		КонецЕсли;
		Аппенедр.УстановитьСвойство(КиЗ.Ключ, КиЗ.Значение);
	КонецЦикла;

	Лог.ДобавитьСпособВывода(Аппенедр, Уровень);
КонецПроцедуры