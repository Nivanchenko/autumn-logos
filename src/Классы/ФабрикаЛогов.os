#Использовать logos
#Использовать autumn

Перем НастройкиЛогов;
Перем Раскладки;

&Дуб
Процедура ПриСозданииОбъекта(
							&Деталька(Значение = "logos.logger", ЗначениеПоУмолчанию = Неопределено) НастройкиУровнейЛогов,
							&Пластилин Поделка
							)
	НастройкиЛогов = ?(НастройкиУровнейЛогов = Неопределено, Новый Соответствие(), НастройкиУровнейЛогов);
	ЗаполнитьРаскладки(Поделка)
КонецПроцедуры

&Завязь
&Характер("Компанейский")
Функция Лог(&Блестяшка ИмяЛогаПолное, &Блестяшка ИмяЛога) Экспорт

	Лог = Логирование.ПолучитьЛог(ИмяЛогаПолное);
	Раскладка = Раскладки.Получить(СтрЗаменить(ИмяЛога, ".", ""));

	Если Не Раскладка = Неопределено Тогда
		Лог.УстановитьРаскладку(Раскладка);
	КонецЕсли;

	Возврат Лог;

КонецФункции

Процедура ЗаполнитьРаскладки(Поделка)

	Желуди = Поделка.НайтиЖелуди("РаскладкаЛогоса",, ТипыПрилепляемыхЖелудей.Соответствие());
	
	Раскладки = Новый Соответствие();
	Для каждого КлючИЗначение Из Желуди Цикл
		Раскладки.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;

	Для Каждого ТекущийЛог Из НастройкиЛогов Цикл
		Если НЕ ТипЗнч(ТекущийЛог.Значение) = Тип("Строка") Тогда
			ИмяКласса = ТекущийЛог.Значение["classlayout"];

			Если НЕ ИмяКласса = Неопределено Тогда
				Раскладки.Вставить(СтрЗаменить(ТекущийЛог.Ключ, ".", ""), Поделка.НайтиЖелудь(ИмяКласса));
			КонецЕсли;

		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

